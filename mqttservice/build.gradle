plugins {
    id 'com.android.library'
    id 'maven-publish'
}

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 1

def computeVersionName() {
    def snapshotTag = ""
    if (mavenRepoType == "snapshots")
        snapshotTag = "-SNAPSHOT"
    return String.format('%d.%d.%d%s', versionMajor, versionMinor, versionPatch, snapshotTag)
}

def computeVersionCode() {
    return (versionMajor * 1000000) + (versionMinor * 100000) + (versionPatch * 10000) + Integer.valueOf(System.env.BUILD_NUMBER ?: 0)
}

android {
    namespace = 'com.tworx.eud'
    compileSdk 31

    defaultConfig {
        minSdk 28
        targetSdk 31
        versionName computeVersionName()
        versionCode computeVersionCode()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.tworx.eud'
                artifactId = 'mqttdroid'
                version = android.defaultConfig.versionName
            }
        }
        repositories {
            maven {
                name = 'tworxrepo'
                allowInsecureProtocol = true
                url tworxrepoUrl
                credentials {
                    username tworxrepoUser
                    password tworxrepoPwd
                }
            }
        }
    }
}